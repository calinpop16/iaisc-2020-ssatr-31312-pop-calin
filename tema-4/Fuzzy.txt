FUNCTION_BLOCK FuzzyController // Block definition (there may be more than one block per file)

VAR_INPUT // Define input variables
targetTemp : REAL;
currentTemp : REAL;
END_VAR

VAR_OUTPUT // Define output variable
command : REAL;
END_VAR

FUZZIFY targetTemp
TERM tooColdTemp := (0, 1) (5, 0);
TERM coldTemp := (0, 0) (5, 1) (10, 1) (15, 0);
TERM warmTemp := (10, 0) (15, 1) (20, 1) (25, 0);
TERM hotTemp := (20, 0) (25, 1) (30, 1) (35, 0);
TERM tooHotTemp := (30, 0) (35, 1);
END_FUZZIFY

FUZZIFY currentTemp
TERM tooColdTemp := (0, 1) (5, 0);
TERM coldTemp := (0, 0) (5, 1) (10, 1) (15, 0);
TERM warmTemp := (10, 0) (15, 1) (20, 1) (25, 0);
TERM hotTemp := (20, 0) (25, 1) (30, 1) (35, 0);
TERM tooHotTemp := (30, 0) (35, 1);
END_FUZZIFY

DEFUZZIFY command
TERM heat := (0,0) (10, 1) (15, 0);
TERM noChange:= (15,0) (20,1) (25,0);
TERM cool := (25,0) (30,1) (35,0);


METHOD : COG; // Use 'Center Of Gravity' defuzzification method
DEFAULT := 0; // Default value is 0 (if no rule activates defuzzifier)
END_DEFUZZIFY

RULEBLOCK No1
AND : MIN; // Use 'min' for 'and' (also implicit use 'max' for 'or' to fulfill DeMorgan's Law)
ACT : MIN; // Use 'min' activation method
ACCU : MAX; // Use 'max' accumulation method

RULE 1 : IF currentTemp IS tooColdTemp AND targetTemp IS tooColdTemp THEN command IS noChange;
RULE 2 : IF currentTemp IS tooColdTemp AND targetTemp IS NOT tooColdTemp THEN command IS heat;
RULE 3 : IF currentTemp IS coldTemp AND targetTemp IS tooColdTemp THEN command IS cool;
RULE 4 : IF currentTemp IS coldTemp AND targetTemp IS coldTemp THEN command IS noChange;
RULE 5 : IF currentTemp IS coldTemp AND targetTemp IS warmTemp THEN command IS heat;
RULE 6 : IF currentTemp IS coldTemp AND targetTemp IS hotTemp THEN command IS heat;
RULE 7 : IF currentTemp IS coldTemp AND targetTemp IS tooHotTemp THEN command IS heat;
RULE 8 : IF currentTemp IS warmTemp AND targetTemp IS tooColdTemp THEN command IS cool;
RULE 9 : IF currentTemp IS warmTemp AND targetTemp IS coldTemp THEN command IS cool;
RULE 10 : IF currentTemp IS warmTemp AND targetTemp IS warmTemp THEN command IS noChange;
RULE 11 : IF currentTemp IS warmTemp AND targetTemp IS hotTemp THEN command IS heat;
RULE 12 : IF currentTemp IS warmTemp AND targetTemp IS tooHotTemp THEN command IS heat;
RULE 13 : IF currentTemp IS hotTemp AND targetTemp IS tooColdTemp THEN command IS cool;
RULE 14 : IF currentTemp IS hotTemp AND targetTemp IS coldTemp THEN command IS cool;
RULE 15 : IF currentTemp IS hotTemp AND targetTemp IS warmTemp THEN command IS cool;
RULE 16 : IF currentTemp IS hotTemp AND targetTemp IS hotTemp THEN command IS noChange;
RULE 17 : IF currentTemp IS hotTemp AND targetTemp IS tooHotTemp THEN command IS heat;
RULE 18 : IF currentTemp IS tooHotTemp AND targetTemp IS tooHotTemp THEN command IS noChange;
RULE 19 : IF currentTemp IS tooHotTemp AND targetTemp IS NOT tooHotTemp THEN command IS cool;


END_RULEBLOCK

END_FUNCTION_BLOCK